ext {
    println("gradle version is ${gradleVersion}")
    println("jdk version is ${javaVersion}")
    println("kotlin version is ${kotlinVersion}")
    println("compile : detected JAVA_HOME=$System.env.JAVA_HOME")
    println("encoding : ${encoding}")
}

buildscript {
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}"
    }
    repositories {
        mavenLocal()
        maven { url "${mavenCentralUrl}" }
        mavenCentral()
        jcenter()
    }
}

configure(allprojects) {

    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'kotlin-kapt'
    apply plugin: 'kotlin-spring'
    apply plugin: 'kotlin-noarg'
    apply plugin: 'kotlin-jpa'
    apply plugin: 'idea'
    apply plugin: 'maven'

    apply from: "$rootDir/gradle/dependencies.gradle"

    sourceCompatibility = "${javaVersion}"
    targetCompatibility = "${javaVersion}"

    buildscript {
        repositories {
            mavenLocal()
            maven { url "${mavenCentralUrl}" }
            mavenCentral()
            jcenter()
        }
    }

    repositories {
        mavenLocal()
        maven { url "${mavenCentralUrl}" }
        mavenCentral()
        jcenter()
    }

    dependencies {
        testCompile("org.junit.jupiter:junit-jupiter-engine:$versions.jupiter")
        testCompile("org.junit.jupiter:junit-jupiter-api:$versions.jupiter")
        testCompile "org.jetbrains.kotlin:kotlin-test"
        testCompile "org.jetbrains.kotlin:kotlin-test-junit5"
        compile "org.jetbrains.kotlin:kotlin-stdlib"
        compile "org.jetbrains.kotlin:kotlin-reflect"
        compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.kotlin_coroutines"
    }

    kapt {
        javacOptions {
            // 增加注解处理器的最大错误次数
            // 默认为 100。
            option("-Xmaxerrs", 500)
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "${encoding}"
    }
    tasks.withType(Javadoc) {
        options.encoding = "${encoding}"
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    task clearPj(type: Delete) {
        delete 'build', 'target', 'out'
    }

    task copyJar(type: Copy) {
        from(configurations.runtime)
        into('build/libs/dependencies')
    }

    task mapperFileCopy(type: Copy) {
        copy {
            from("src/main/java") {
                include("**/*.xml")
                include("**/*.json")
                include("**/*.properties")
            }
            into("${buildDir}/classes/main")
        }
        copy {
            from("src/test/java") {
                include("**/*.xml")
                include("**/*.json")
                include("**/*.properties")
            }
            into("${buildDir}/classes/test")
        }
        copy {
            from("src/main/kotlin") {
                include("**/*.xml")
                include("**/*.json")
                include("**/*.properties")
            }
            into("${buildDir}/classes/main")
        }
        copy {
            from("src/test/kotlin") {
                include("**/*.xml")
                include("**/*.json")
                include("**/*.properties")
            }
            into("${buildDir}/classes/test")
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set("sources")
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set("javadoc")
        from javadoc.destinationDir
    }

    task release(type: Copy, dependsOn: [build, copyJar]) {
    }

    jar {
        dependsOn(copyJar)
        manifest {
            attributes('Version': "${version}", 'Provider': "Gradle ${gradleVersion}", 'JDK': "${javaVersion}", 'Kotlin': "${kotlinVersion}", 'Project': 'Acp Admin Cloud')
        }
    }

    javadoc {
        options.charSet = "${encoding}"
        options.docEncoding = "${encoding}"
        options.encoding = "${encoding}"
    }

    compileJava {
        options.encoding = "${encoding}"
    }
    compileJava.dependsOn(processResources)

    compileTestJava {
        options.encoding = "${encoding}"
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "${javaVersion}"
        }
    }
    compileKotlin.dependsOn(processResources)

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "${javaVersion}"
        }
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'// no caching to maven repository
    }

    def generateGroupId = {
        def groupId = "$project.group"
        if (project.parent != null) {
            def parentPath = project.parent.path.substring(1)
            if (!parentPath.isBlank()) {
                groupId += "." + parentPath.replace(":", ".")
            }
        }
        return groupId
    }

    // install to local repository
    install {
        dependsOn release
        repositories.mavenInstaller {
            def groupId = generateGroupId()
            pom.version = "$project.version"
            pom.artifactId = "$project.name"
            pom.groupId = groupId
            doLast {
                println("install archive to local: group=$pom.groupId , artifact=$pom.artifactId , version=$pom.version")
            }
        }
    }

    // deploy to remote repository
    uploadArchives {
        dependsOn install
        apply from: "$rootDir/gradle/deploy.gradle"
        repositories {
            mavenDeployer {
                // remote repository url
                repository(url: mavenUploadUrl) {
                    // loginName and password
                    authentication(userName: mavenUserName, password: mavenPassword)
                }
                def groupId = generateGroupId()
                pom.version = "$project.version"
                pom.artifactId = "$project.name"
                pom.groupId = groupId
                doLast {
                    println("deploy archive to remote : $mavenUploadUrl")
                    println("group=$pom.groupId , artifact=$pom.artifactId , version=$pom.version")
                }
            }
        }
    }

}